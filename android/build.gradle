apply plugin: 'com.android.application'

buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:2.3.0'
		classpath 'net.sf.proguard:proguard-gradle:5.3.3'
	}
}

allprojects {
	repositories {
		jcenter()
	}
}

def getVersionCode = { ->

	def code = project.hasProperty('versionCode') ? versionCode.toInteger() : 1
	println "VersionCode is set to $code"
	return code
}

def getVersionName = { ->

	def name = project.hasProperty('versionName') ? versionName : "1.0"
	println "VersionName is set to $name"
	return name
}

def keystorePropertiesFile = project.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
	compileSdkVersion 25
	buildToolsVersion "25.0.2"
	defaultConfig {
		applicationId "com.eegeo.ProjectSwallowApp"
		minSdkVersion 21
		targetSdkVersion 24
		multiDexEnabled true
		versionCode getVersionCode()
		versionName getVersionName()
		externalNativeBuild {
			cmake {
                cppFlags "-g -Wno-expansion-to-defined -Wno-format-security"
            }
		}
		ndk {
			abiFilters 'armeabi-v7a'
		}
	}

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
			keyPassword System.getenv("ANDROID_SIGNING_KEY_PASSWORD")
            storeFile file(keystoreProperties['storeFile'])
			storePassword System.getenv("ANDROID_SIGNING_STORE_PASSWORD")
        }
    }

	buildTypes {
		release {
			minifyEnabled true
			proguardFiles 'proguard-rules.pro', getDefaultProguardFile('proguard-android-optimize.txt')
			signingConfig signingConfigs.release
		}

		debug {
            jniDebuggable true
            debuggable true
			minifyEnabled false
		}
	}

	externalNativeBuild {
		cmake {
			path "CMakeLists.txt"
		}
	}

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			res.srcDirs = ['res']
			assets.srcDirs = ['assets']
			jniLibs.srcDirs = ['libs']
		}
	}

	productFlavors {
		armv7a {
			dimension "abi"
			ndk { abiFilter "armeabi-v7a" }
		}
	}
}

dependencies {
	compile fileTree(include: ['*.jar'], dir: 'libs')
	compile 'com.android.support:multidex:1.0.1'
	compile project(':wrld-android-widgets-release')
}
