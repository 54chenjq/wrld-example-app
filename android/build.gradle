apply plugin: 'com.android.application'

buildscript {
	repositories {
		jcenter()
        google()
    }

	dependencies {
		classpath 'com.android.tools.build:gradle:3.1.1'
	}
}

allprojects {
	repositories {
		jcenter()
        google()
    }
}

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.eegeo.mobileexampleapp"
        minSdkVersion 19
        targetSdkVersion 27
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
		
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                cppFlags "-Wall", "-Wno-unknown-pragmas", "-Wno-sign-compare", "-Wno-format-security",
                         "-Wno-reorder", "-Wno-expansion-to-defined", "-frtti", "-fexceptions", "-std=c++11",
                         "-D__STDC_FORMAT_MACROS"
                arguments "-DANDROID=1", "-DANDROID_TOOLCHAIN=clang", "-DANDROID_ARM_MODE=arm"
            }

            ndk {
                abiFilters "armeabi", "armeabi-v7a", "arm64-v8a"
            }
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
            java.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }

        androidTest {
            java.srcDirs = ['tests']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            externalNativeBuild {
                cmake {
                    cppFlags "-g"
                    arguments "-DDEBUG=1"
                }
            }

            jniDebuggable true
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "main"

    productFlavors {
        normal {
            dimension "main"
        }

        screenshot {
            dimension "main"

            externalNativeBuild {
                cmake {
                   cppFlags "-DAUTOMATED_SCREENSHOTS=1"
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            // Set path to external eegeo-mobile top CMakeLists here and in sdk/CMakeLists.txt
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    // compile 'com.android.support:support-core-utils:25.2.0'
    implementation 'com.android.support:multidex:1.0.1'
    implementation project(':wrld-navigation-release')
    implementation project(':wrld-navigation-widget-release')

    androidTestImplementation 'tools.fastlane:screengrab:1.0.2'
    androidTestImplementation 'com.android.support:appcompat-v7:27.1.1'
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test:rules:0.5'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation project(':wrld-navigation-release')
    androidTestImplementation project(':wrld-navigation-widget-release')
}
